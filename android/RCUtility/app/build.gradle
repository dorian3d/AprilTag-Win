apply plugin: 'com.android.model.application'

model {
    android {
        compileSdkVersion = 'Intel Corporation:Intel RealSense Add-on:22'
        buildToolsVersion = "22.0.1"
        defaultConfig.with {
            applicationId = "com.realitycap.android.rcutility"
            minSdkVersion.apiLevel = 22
            targetSdkVersion.apiLevel = 22
            version = 1
        }
    }
    android.lintOptions {
        abortOnError = false
        warningsAsErrors = false
    }
    android.ndk {
        moduleName = "tracker_wrapper"
        ldLibs.addAll "log", "GLESv2"
        stl = "c++_shared"
        cppFlags.add "-I" + file("src/main/jni")
        debuggable = true
    }
    android.buildTypes {
        def debugSigningConfig
        debug {
            debuggable = true
            debugSigningConfig = signingConfig
        }
        release {
            signingConfig = debugSigningConfig
        }
        create("relwithdebinfo") {
            debuggable = true
            signingConfig = debugSigningConfig
        }
    }
    android.productFlavors {
        create("rc86") {
            ndk.with {
                abiFilters.add "x86"
            }
        }
        create("rc64") {
            ndk.with {
                 abiFilters.add "x86_64"
            }
        }
    }
}

project.ext.ndkDir = new com.android.build.gradle.internal.NdkHandler(rootDir, null, "clang", "3.6").getNdkDirectory()

if (file("../../rc_replay/lib/").exists())
    model {
        android.ndk {
            cppFlags.add "-I" + file("../../rc_replay/src")
            ldLibs.add "tracker"
        }
        android.sources {
            main { jni { source { srcDirs = [ "../../rc_replay/src", "src/main/jni" ] } } }
            main { jniLibs { source { srcDirs = [ "../../rc_replay/lib", "src/main/jniLibs" ] } } }
        }
        android.productFlavors {
            rc86 { ndk.ldFlags.add "-L" + file("../../rc_replay/lib/x86") }
            rc64 { ndk.ldFlags.add "-L" + file("../../rc_replay/lib/x86_64") }
        }
    }
else
    apply from: "../../cmake.gradle"

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def rsRoot = properties.getProperty('sdk.dir')

repositories {
    flatDir {
        dirs rsRoot + '/extras/intel/RealSense_SDK/libs'
    }
}

dependencies {
    compile 'com.android.support:support-v13:22.2.0'
    compile(name:'PXCACore', ext:'aar')
    compile(name:'PXCASenseManager', ext:'aar')
    compile(name:'PXCAPlayback', ext:'aar')
}
